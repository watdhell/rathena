//===== rAthena Script ======================================= 
//= Shops
//===== By: EderRock =========================================  
//= rAthena New Scripter
//===== Current Version: ===================================== 
//= 1.8 - Edição e organização gerais do código
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= NPC que fornece buffs, recuperação de HP/SP, funcionalidades de 
//= identificação/reparo de itens, e um sistema de níveis de acesso.
//===== Changelog: =========================================== 
//= 1.0 -		Buffs e cura HP/SP.
//= 1.1 -		Adicionado array de buffs.
//= 1.1.1 -		Opção para contrato adicionada.
//= 1.2 -		Adicionado sistema de níveis de buff.
//= 1.2.1 -		Adicionado função para criar e armazenar em
// = 			banco de dados.
//= 1.2.1.1 -	Removida opção de armazenamento em variáveis.
//= 1.2.2 -		Adicionada a possibilidade de desativar os buffs.
//= 1.3 -		Adicionado opção de testar os buffs por 1 minuto.
//= 1.4 -		Adicionados cutins e menus personalizados.
//= 1.5 -		Adicionados espíritos de classes.
//= 1.6 -		Adicionada a opção de identificar/reparar itens.
//= 1.7 -		Editadas opções de identificar/reparar itens e
// =			espíritos de classes por nível de acesso.
//= 1.8 - Edição e organização gerais do código.
//============================================================

prontera,165,170,4	script	Katsua	4_F_02,{
    // Configurações Variáveis
    set .@npc_name$, "^FF0000Celestia^000000";  // Nome do NPC com cor vermelha
    set .@pagamento_tipo, 1;                    // Tipo de pagamento (0 = zeny, 1 = cash)
    set .@contract_cost, 10000;                 // Custo do contrato
    set .@buff_cost, 0;                         // Custo do buff
    set .@vip_disc, 20;                         // Desconto para VIP (%)
    set .@cont_dur, 1;                          // Duração do contrato em dias
    set .@use_vip_time, 0;                      // Usar tempo VIP para duração do contrato (0 = não, 1 = sim)
    set .@adm_level, 99;                        // Nível de acesso do administrador
    set .@buff_dur_no_contract, 10;             // Duração do buff sem contrato (em minutos)
    set .@buff_dur_with_contract, 30;           // Duração do buff com contrato (em minutos)
    set .@time_interval, 5;						// Variável global para controlar o intervalo de uso em segundos
	set enable_spirit_buffs, 1;                 // 1 = habilitado, 0 = desabilitado
	set .@repair_access_level, 2;				// Nível de acesso necessário para reparo
	set .@identify_access_level, 2;				// Nível de acesso necessário para identificação
    set .bcor$, "^008aff[»]^000000 ";           // Cor do botão para opções de menu
    set .rcor$, "^ff0000[»]^000000 ";           // Cor do botão para a opção de cancelar

    cutin "katsua01", 2;
    mes "[" + .@npc_name$ + "]";
    mes " ";
    mes "Olá " + strcharinfo(0) + "! Como posso te ajudar?";
    next;

    query_sql "SELECT UNIX_TIMESTAMP(contract_end) FROM buff_contract WHERE account_id = " + getcharid(3), .@contract_end$;
if (gettimetick(2) < atoi(.@contract_end$)) {
    switch (select(.bcor$+"Me abençoe", .rcor$+"Cancelar")) {
        case 1: goto BlessMe;
        case 2: callfunc("cancelar_sair", .@npc_name$);
    }
} else {
    switch (select(.bcor$+"Me abençoe", .bcor$+"Saber sobre a doação", .rcor$+"Cancelar")) {
        case 1: goto BlessMe;
        case 2: goto ContractInfo;
        case 3:callfunc("cancelar_sair", .@npc_name$);
    }
}

BlessMe:
    cutin "katsua02", 2;
    if (gettimetick(2) - #LastUseTime < .@time_interval) {
        set .@wait_time, .@time_interval - (gettimetick(2) - #LastUseTime);
        dispbottom "Por favor, aguarde " + callfunc("format_timer", .@wait_time) + " antes de usar novamente.";
        close2;
        cutin "", 255;
        end;
    }
    if (getgmlevel() >= .@adm_level) {
        callfunc "apl_blessing", 60, 100, 0;
        callfunc "apl_repair_identify", 100, 100;
        dispbottom "Você é um administrador. Aplicando as bençãos por 1 hora!";
        end;
    }
    if (vip_status(1) && #HasActiveContract == 1) {
        callfunc "apl_blessing", .@buff_dur_with_contract, 2, 0;
        callfunc "apl_repair_identify", .@repair_access_level, .@identify_access_level;
        dispbottom "Você é VIP e tem um contrato ativo. Esta benção estará contigo por " + .@buff_dur_with_contract + " minutos.";
        set #LastUseTime, gettimetick(2);
        end;
    }
    if (vip_status(1) || #HasActiveContract == 1) {
        callfunc "apl_blessing", .@buff_dur_with_contract, 1, 0;
        callfunc "apl_repair_identify", .@repair_access_level, .@identify_access_level;
        dispbottom "Você é VIP ou tem um contrato ativo. Esta benção estará contigo por " + .@buff_dur_with_contract + " minutos.";
        set #LastUseTime, gettimetick(2);
        end;
    } else {
        callfunc "apl_blessing", .@buff_dur_no_contract, 0, 0;
        callfunc "apl_repair_identify", .@repair_access_level, .@identify_access_level;
        dispbottom "Você não tem um contrato ativo. Esta benção estará contigo por " + .@buff_dur_no_contract + " minutos.";
        set #LastUseTime, gettimetick(2);
        end;
    }

ContractInfo:
    cutin "katsua02", 2;
    mes "[" + .@npc_name$ + "]";
    mes " ";
    query_sql "SELECT UNIX_TIMESTAMP(contract_end) FROM buff_contract WHERE account_id = " + getcharid(3), .@contract_end$; 
    if (gettimetick(2) < atoi(.@contract_end$)) {
        set .@rem_time, atoi(.@contract_end$) - gettimetick(2);
        mes "Oba!";
        mes "Vejo que você já fez uma doação.";
        mes "Irei interceder por você por mais";
        mes " ";
        mes "^FF0000" + callfunc("format_timer", .@rem_time) + "^000000.";
        close;
    }
    if (.@contract_end$ == "") {
        mes "Você não tem um contrato ativo.";
        mes "Por uma doação de míseros " + callfunc("format_value", .@contract_cost) + " " + (.@pagamento_tipo == 0 ? "zeny" : "cash") + ", ofereço:";
        mes "Buffs exclusivos por " + (.@cont_dur * 24) + " horas.";
        mes "Aceita?";
        switch (select(.bcor$+"Fazer Doação", .bcor$+"Ver benefícios", .rcor$+"Cancelar")) {
            case 1: goto AssinarContrato;
            case 2: goto BuffForAMinute;
            case 3: callfunc("cancelar_sair", .@npc_name$);
        }
    } else {
        mes "Seu contrato expirou. Você pode assinar um novo contrato.";
        mes "Por uma doação de míseros " + callfunc("format_value", .@contract_cost) + " " + (.@pagamento_tipo == 0 ? "zeny" : "cash") + ", ofereço:";
        mes "Buffs exclusivos por " + (.@cont_dur * 24) + " horas.";
        mes "Aceita a oferta?";
        next;
        switch (select(.bcor$+"Fazer Doação", .bcor$+"Ver benefícios", .rcor$+"Cancelar")) {
            case 1: goto AssinarContrato;
            case 2: goto BuffForAMinute;
            case 3: callfunc("cancelar_sair", .@npc_name$);
        }
    }
    
AssinarContrato:
	cutin "katsua03", 2;
    mes "[" + .@npc_name$ + "]";
    mes " ";
    if ((.@pagamento_tipo == 0 && Zeny < .@contract_cost) || (.@pagamento_tipo == 1 && #CASHPOINTS < .@contract_cost)) {
		clear;
		mes "[" + .@npc_name$ + "]";
		mes " ";
        mes "Você não tem " + (.@pagamento_tipo ? "cash" : "zeny") + " suficiente.";
		close2;
		cutin "", 255;
		end;
    }
    if (.@pagamento_tipo == 0) {
        set Zeny, Zeny - .@contract_cost;
        dispbottom "Contrato fechado! " + .@contract_cost + " Zeny deduzido. Saldo restante: " + Zeny + " Zeny.";
    } else {
        set #CASHPOINTS, #CASHPOINTS - .@contract_cost;
        dispbottom "Contrato fechado! " + .@contract_cost + " Cash deduzido. Saldo restante: " + #CASHPOINTS + " Cash.";
    }
    callfunc "criar_ctt_healler", .@cont_dur;
	cutin "katsua02", 2;
	clear;
	mes "[" + .@npc_name$ + "]";
    mes " ";
    mes "Você pode usar meus serviços sem custos até o final do seu contrato.";
	close2;
	cutin "", 255;
	end;

BuffForAMinute:
    callfunc "remove_buffs";
    callfunc "apl_blessing", 1, 1, 1;
    set #MinutoBuff, 1;
    dispbottom "Buffs aplicados! Eles irão durar 1 minuto.";
		cutin "katsua02", 2;
		mes "[" + .@npc_name$ + "]";
		mes " ";
		mes "Você foi abençoado, porém";
		mes "Se você finalizar esta conversa,";
		mes "as bênçãos serão removidas.";
		mes "^FF0000Você está congelado por 1 minuto^000000";
		mes "^FF0000ou até finalizar a conversa!^000000";
    switch (select(.bcor$+"Fazer doação", .rcor$+" Encerrar a conversa")) {
        case 1:
            cutin "katsua03", 2;
            callfunc "remove_buffs";
            mes "[" + .@npc_name$ + "]";
            mes "As bênçãos foram removidas!";
            dispbottom "As bênçãos foram removidas!";
            set #MinutoBuff, 0;
            goto AssinarContrato;
        case 2:
            cutin "katsua01", 2;
            clear;
            callfunc "remove_buffs";
            mes "[" + .@npc_name$ + "]";
            mes "Espero que tenha sido útil! Volte se desejar assinar o contrato.";
            dispbottom "As bênçãos foram removidas!";
            set #MinutoBuff, 0;
            close2;
            cutin "", 255;
            end;
    }
OnBuffEnd:
    callfunc "remove_buffs";
    set #MinutoBuff, 0;
    end;

OnPCLoginEvent:
    callfunc("verificar_ctt_healler");
    end;

OnInit:
    query_sql "CREATE TABLE IF NOT EXISTS buff_contract (account_id INT NOT NULL, contract_end DATETIME NOT NULL, PRIMARY KEY (account_id))"; 
    end;
}

function	script	apl_blessing	{
    set .@dur, getarg(0) * 60;
    set .@access_level, getarg(1);
    set .@mode, getarg(2);


    setarray .@buffs[0],// maximo de 42 buffs totais
        1, 1, 10,  // [1]  Adrenalina - Aumenta a velocidade de ataque
        1, 0, 10,  // [2]  Angelus - Aumenta a defesa
        1, 0, 10,  // [3]  Aumentar Agilidade - Aumenta a agilidade
        1, 0, 10,  // [4]  Benção - Aumenta força, agilidade e inteligência
        1, 2, 10,  // [5]  Caminho do Vento - Aumenta a velocidade de movimento
        1, 1, 10,  // [6]  Concentração - Aumenta precisão e esquiva
        1, 4, 30,  // [7]  Comida de Esquiva - Aumenta a esquiva
        1, 4, 5,   // [8]  Comida de FOR - Aumenta a força
        1, 4, 5,   // [9]  Comida de AGI - Aumenta a agilidade
        1, 4, 5,   // [10] Comida de VIT - Aumenta a vitalidade
        1, 4, 5,   // [11] Comida de INT - Aumenta a inteligência
        1, 4, 5,   // [12] Comida de DES - Aumenta a destreza
        1, 4, 5,   // [13] Comida de SOR - Aumenta a sorte
        1, 4, 5,   // [14] Comida de Precisão - Aumenta a precisão
        1, 4, 5,   // [15] Comida de ATQ - Aumenta o ataque físico
        1, 4, 5,   // [16] Comida de MATQ - Aumenta o ataque mágico
        1, 4, 5,   // [17] Derretimento - Reduz a defesa do inimigo
        1, 4, 5,   // [18] EDP (Envenom Deadly Poison) - Aumenta o dano de ataque
        1, 1, 5,   // [19] Glória - Aumenta a sorte
        1, 4, 5,   // [20] Impositio - Aumenta o ataque físico
        1, 4, 7,   // [21] Kaahi - Recupera HP ao ser atacado
        1, 4, 7,   // [22] Kaite - Reflete dano mágico
        1, 4, 3,   // [23] Kaupe - Evita um ataque
        0, 4, 7,   // [24] Kaizel - Revive automaticamente
        1, 4, 5,   // [25] Lâmina de Aura - Aumenta o ataque físico
        1, 1, 5,   // [26] Magnificat - Aumenta a regeneração de SP
        1, 4, 1,   // [27] Manto de Energia - Reduz o dano recebido
        0, 3, 5,   // [28] Maximizar Poder - Maximiza o dano de ataque
        1, 4, 5,   // [29] Overthrust - Aumenta o ataque físico
        1, 3, 1,   // [30] Poção de ASPD - Aumenta a velocidade de ataque
        1, 4, 1,   // [31] Grito - Aumenta a força
        1, 4, 10,  // [32] Perfeição de Arma - Aumenta a precisão do ataque
        1, 4, 10,  // [33] Resistir - Aumenta a resistência a status negativos
        1, 2, 3,   // [34] Suffragium - Reduz o tempo de conjuração
        1, 2, 10;  // [35] Kyrie Eleison - Bloqueia dano físico
    setarray .@buff_ids, SC_ADRENALINE2, SC_ANGELUS, SC_INCREASEAGI, SC_BLESSING, 
                        SC_WINDWALK, SC_CONCENTRATE, SC_FLEEFOOD, SC_STRFOOD, 
                        SC_AGIFOOD, SC_VITFOOD, SC_INTFood, SC_DEXFood, 
                        SC_LUKFood, SC_HitFood, SC_BATKFood, SC_MATKFood, 
                        SC_MELTDOWN, SC_EDP, SC_GLORIA, SC_IMPOSITIO, 
                        SC_KAAHI, SC_KAITE, SC_KAUPE, SC_KAIZEL, 
                        SC_AURABLADE, SC_MAGNIFICAT, SC_ENERGYCOAT, SC_MAXIMIZEPOWER, 
                        SC_OVERTHRUST, SC_ASPDPOTION2, SC_LOUD, SC_WEAPONPERFECTION, 
                        SC_ENDURE, SC_SUFFRAGIUM, SC_KYRIE;
    for (set .@i, 0; .@i < getarraysize(.@buffs); set .@i, .@i + 3) {
        if (.@buffs[.@i] == 1 && .@buffs[.@i + 1] <= .@access_level) {
            sc_end SC_STONE;
            if (getgmlevel() >= .@access_level) {
                sc_start(.@buff_ids[.@i / 3], 3600000, .@buffs[.@i + 2]);
            } else {
                sc_start(.@buff_ids[.@i / 3], .@dur * 1000, .@buffs[.@i + 2]);
            }
        }
    }
    if (enable_spirit_buffs && .@access_level >= .@spirit_buffs_access_level) {
        callfunc("apply_spirit_buffs", .@dur, .@access_level);
    }
    if (.@mode == 1) {
        sc_start SC_STONE, 60000, 1;
        dispbottom "Stone Curse aplicado com Buffs!";
    }
    cutin "katsua02", 2;
    percentheal 100, 100;
    dispbottom "Você foi abençoado!";
    close2;
    cutin "", 255;
    return;
}

function	script	apply_spirit_buffs	{
    set .@dur, getarg(0);
    set .@access_level, getarg(1);
    if (enable_spirit_buffs && .@access_level >= .@spirit_buffs_access_level) {
        set .@spirit, 0;
        switch (basejob) {
            case Job_Alchemist:         set .@spirit, 445; break;
            case Job_Monk:              set .@spirit, 447; break;
            case Job_Star_Gladiator:    set .@spirit, 448; break;
            case Job_Sage:              set .@spirit, 449; break;
            case Job_Crusader:          set .@spirit, 450; break;
            case Job_SuperNovice:       set .@spirit, 451; break;
            case Job_Knight:            set .@spirit, 452; break;
            case Job_Wizard:            set .@spirit, 453; break;
            case Job_Priest:            set .@spirit, 454; break;
            case Job_Bard:              set .@spirit, 455; break;
			case Job_Dancer:            set .@spirit, 455; break;
            case Job_Rogue:             set .@spirit, 456; break;
            case Job_Assassin:          set .@spirit, 457; break;
            case Job_Blacksmith:        set .@spirit, 458; break;
            case Job_Hunter:            set .@spirit, 460; break;
            case Job_Soul_Linker:       set .@spirit, 461; break;
        }
        if (.@spirit) {
            if (.@access_level == 100) {
                sc_start4 SC_SPIRIT, 3600000, 5, .@spirit, 0, 0;
            } else {
                sc_start4 SC_SPIRIT, .@dur * 1000, 5, .@spirit, 0, 0;
            }
        }
    }
    return;
}

function	script	criar_ctt_healler	{
    set .@contract_end$, gettimetick(2) + (getarg(0) * 86400);
    query_sql "REPLACE INTO buff_contract (account_id, contract_end) VALUES (" + getcharid(3) + ", FROM_UNIXTIME(" + .@contract_end$ + "))";
    set #HasActiveContract, 1;
	cutin "", 255;
	clear;
    return;
}

function	script	verificar_ctt_healler	{
    query_sql "SELECT UNIX_TIMESTAMP(contract_end) FROM buff_contract WHERE account_id = " + getcharid(3), .@contract_end$;
    if (.@contract_end$ != "" && gettimetick(2) < atoi(.@contract_end$)) {
        set #HasActiveContract, 1;
    } else {
        set #HasActiveContract, 0;
    }
    if (vip_status(1)) {
        set #HasActiveVIP, 1;
    } else {
        set #HasActiveVIP, 0;
    }
    return;
}

function	script	remove_buffs	{
    setarray .@buff_ids, SC_ADRENALINE2, SC_ANGELUS, SC_INCREASEAGI, SC_BLESSING, 
                        SC_WINDWALK, SC_CONCENTRATE, SC_FLEEFOOD, SC_STRFOOD, 
                        SC_AGIFOOD, SC_VITFOOD, SC_INTFood, SC_DEXFood, 
                        SC_LUKFood, SC_HitFood, SC_BATKFood, SC_MATKFood, 
                        SC_MELTDOWN, SC_EDP, SC_GLORIA, SC_IMPOSITIO, 
                        SC_KAAHI, SC_KAITE, SC_KAUPE, SC_KAIZEL, 
                        SC_AURABLADE, SC_MAGNIFICAT, SC_ENERGYCOAT, SC_MAXIMIZEPOWER, 
                        SC_OVERTHRUST, SC_ASPDPOTION2, SC_LOUD, SC_WEAPONPERFECTION, 
                        SC_ENDURE, SC_SUFFRAGIUM, SC_KYRIE, SC_SPIRIT;
    for (set .@i, 0; .@i < getarraysize(.@buff_ids); set .@i, .@i + 1) {
        sc_end .@buff_ids[.@i];
    }
    sc_end SC_STONE;
    return;
}

function	script	cancelar_sair	{
    clear;
    cutin "katsua03", 2;
    mes "[" + getarg(0) + "]";
    mes " ";
    mes "Tudo bem, volte quando precisar!";
    close2;
    cutin "", 255;
    end;
}

function	script	apl_repair_identify	{
    set .@repair_access_level, getarg(0);
    set .@identify_access_level, getarg(1);
    if (.@repair_access_level >= 2 && getbrokenid(1)) {
        repairall;
    }
    if (.@identify_access_level >= 1) {
        getinventorylist;
        for (set .@i, 0; .@i < @inventorylist_count; set .@i, .@i + 1) {
            if (!@inventorylist_identify[.@i]) {
                delitem2 @inventorylist_id[.@i], 1, 0, 0, 0, 0, 0, 0, 0;
                getitem @inventorylist_id[.@i], 1;
            }
        }
    }
    return;
}

function	script	format_timer	{
	set .@time, getarg(0);
	set .@days, .@time / (24 * 60 * 60);
	set .@hours, (.@time % (24 * 60 * 60)) / (60 * 60);
	set .@minutes, (.@time % (60 * 60)) / 60;
	set .@seconds, .@time % 60;
	if (.@days > 0) {
		return .@days + " dias, " + .@hours + " horas, " + .@minutes + " minutos, " + .@seconds + " segundos";
	} else if (.@hours > 0) {
		return .@hours + " horas, " + .@minutes + " minutos, " + .@seconds + " segundos";
	} else if (.@minutes > 0) {
		return .@minutes + " minutos, " + .@seconds + " segundos";
	} else {
		return .@seconds + " segundos";
	}
}

function	script	format_value	{
	set .@value, getarg(0);
	set .@formatted_value$, "";
	while (.@value > 0) {
		set .@remainder, .@value % 1000;
		set .@value, .@value / 1000;
		if (.@value > 0) {
			set .@formatted_value$, "." + (.@remainder < 10 ? "00" : .@remainder < 100 ? "0" : "") + .@remainder + .@formatted_value$;
		} else {
			set .@formatted_value$, .@remainder + .@formatted_value$;
		}
	}
	return .@formatted_value$;
}

sec_in02,33,177,4	duplicate(Katsua)	Katsua#01	4_F_02

